class PsxArachnotron : PsxActor replaces Arachnotron
{
    Default
    {
		//$Title "Arachnotron"
		
        Health 500;
		Radius 64;
		Height 64;
		Mass 600;
		//Speed 12;
		PainChance 128;
		Monster;
		+FLOORCLIP
		+BOSSDEATH
		SeeSound "baby/sight";
		PainSound "baby/pain";
		DeathSound "baby/death";
		ActiveSound "baby/active";
		Obituary "$OB_BABY";
		Tag "$FN_ARACH";

        Speed 6;
        +MOVEWITHSECTOR;
        PsxActor.PCSpeed 12;
        PsxActor.DeathThudSound "monster/xvile";
        +PsxActor.WAITLOOK;
        +PsxActor.SEETARGET;
		PsxActor.NightmareMissileChanceFactor 1.2;
    }

	States
	{
        Spawn:
            BSPI A 0 nodelay a_jumpif(IsSmooth(), "SmoothSpawn");
            BSPI AB 10 A_Look;
            Loop;
        SmoothSpawn:
            SACH ab 10 A_Look;
            loop;

        See:
            TNT1 A 0 a_jumpif(IsSmooth(), "SmoothSee");
            BSPI A 20;
            BSPI A 4 { A_PCTics(3); A_BabyMetal(); }
            BSPI ABBCC 4 { A_PCTics(3); A_Chase(); }
            BSPI D 4 { A_PCTics(3); A_BabyMetal(); }
            BSPI DEEFF 4 { A_PCTics(3); A_Chase(); }
            Goto See+2;
        SmoothSee:
            ARAC A 20;
            ARAC A 4 { A_PCTics(3); A_BabyMetal(); }
            ARAC BCDEE 4 { A_PCTics(3); A_Chase(); }
            ARAC F 4 { A_PCTics(3); A_BabyMetal(); }
            ARAC GHIJJ 4 { A_PCTics(3); A_Chase(); }
            goto SmoothSee+1;

        Missile:
            TNT1 A 0 a_jumpif(IsSmooth(), "SmoothMissile");
            BSPI A 20 BRIGHT A_FaceTarget;
            BSPI G 4 BRIGHT A_BspiAttack;
            BSPI H 4 BRIGHT;
            BSPI H 2 BRIGHT { A_PCTics(1); A_SpidRefire(); }
            Goto Missile+2;
        SmoothMissile:
            DUMB a 20 A_FaceTarget;
            ARAT a 4 bright A_BspiAttack;
            ARAT b 3 bright;
            ARAT c 3 bright { A_PCTics(2); A_SpidRefire(); }
            goto SmoothMissile+1;

        Pain:
            TNT1 A 0 a_jumpif(IsSmooth(), "SmoothPain");
            BSPI I 4 A_PCTics(3);
            BSPI I 4 { A_PCTics(3); A_Pain(); }
            Goto See+2;
        SmoothPain:
            ARPA a 4 A_PCTics(3);
            ARPA b 4 { A_PCTics(3); A_Pain(); }
            goto SmoothSee+1;

        Death:
            TNT1 A 0 a_jumpif(IsSmooth(), "SmoothDeath");
            BSPI J 20 A_Scream;
            BSPI K 6 A_NoBlocking;
            BSPI LMN 6;
            BSPI O 6 A_DeathThud;
            BSPI P -1 A_BossDeath;
            Stop;
        SmoothDeath:
            sarz a 3;
            sarz b 3 A_Scream;
            sarz c 3 A_NoBlocking;
            sarz defgh 3;
            sarz i 3 A_DeathThud;
            sarz jkl 3;
            sarz m -1 A_BossDeath;
            stop;

        XDeath:
			TNT1 A 0 a_jumpif(IsSmooth(), "SmoothXDeath");
            TNT1 A 0 A_JumpIf(!kch_enabled,"Death");
            ARXD AC 4;
			ARXD E 4 { A_GoreExplosion(); A_ZPExplosion(); }
			ARXD G 4 { A_XScream(); A_ZPExplosion(); }
			ARXD I 4 { A_NoBlocking(); A_BossDeath(); }
			ARXD K 4;
			ARXD L -1;
            Stop;
        SmoothXDeath:
			ARXD ABC 2;
			ARXD D 2 A_GoreExplosion;
			ARXD E 2 A_ZPExplosion;
			ARXD F 2 { A_XScream(); A_ZPExplosion(); }
			ARXD G 2 A_ZPExplosion;
			ARXD H 2 A_BossDeath;
			ARXD I 2 A_NoBlocking;
			ARXD J 2;
			ARXD K 2;
			ARXD L -1;
		    Stop;

        Raise:
			TNT1 A 0 a_jumpif(IsSmooth(), "SmoothRaise");
            BSPI P 5;
            BSPI ONMLKJ 5;
            Goto See+2;
        SmoothRaise:
            SARZ MLKJIHGFEDCB 3;
            SARZ A 3;
            Goto SmoothSee+1;
	}

    void A_ZPExplosion()
    {
        if (zp_enabled)
        {
            A_SpawnItemEx("DeathSpawner_Rocket",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
        }
    }
}

class PsxArachnotronPlasma : PsxProjectile replaces ArachnotronPlasma
{
	Default
	{
		Radius 13;
		Height 8;
		//Speed 25;
		Damage 5;
		Projectile;
		+RANDOMIZE
		+ZDOOMTRANS
		RenderStyle "Add";
		Alpha 0.75;
		SeeSound "baby/attack";
		DeathSound "baby/shotx";
       
        Speed 12.5;
        FastSpeed 25;
        PsxActor.PCSpeed 25;
        PsxActor.AmbientSound "weapons/plasmaloop";
	}

	States
	{
        Spawn:
            TNT1 A 0 nodelay a_jumpif(zp_enabled && hq_enabled, "HQSpawn");
            TNT1 A 0 a_jumpif(IsSmooth(), "SmoothSpawn");
            APLS AB 4 BRIGHT A_ZPTrail;
            Loop;
        SmoothSpawn:
            BAPL abcdefgh 2 bright A_ZPTrail;
            Loop;
        HQSpawn:
            TNT1 A 0 { A_SetScale(0.065); A_SetTranslation("ZPBFG"); }
        HQLoop:
            PLS1 ABCDEFGHIJKLMNOPQRSUVWXYZ 1 Bright A_ZPTrail;
            PLS1 ABCDEF 1 Bright A_ZPTrail;
            Loop;

        Death:
            TNT1 A 0 A_StopAmbientSound;
            TNT1 A 0 a_jumpif(zp_enabled && hq_enabled, "HQDeath");
            TNT1 A 0 a_jumpif(IsSmooth(), "SmoothDeath");
            APBX A 4 BRIGHT A_ZPExplosion;
            APBX BCDE 4 BRIGHT;
            Stop;
        SmoothDeath:
            GPLD a 2 bright A_ZPExplosion;
            GPLD bcefghijklm 2 bright;
            Stop;
        HQDeath:
            TNT1 A 4 Light("ARACHPLAS_X1") A_ZPExplosion;
            TNT1 A 4 Light("ARACHPLAS_X2");
            TNT1 A 4 Light("ARACHPLAS_X3");
            TNT1 A 4 Light("ARACHPLAS_X4");
            Stop;
	}

    void A_ZPTrail()
    {
        if (zp_enabled)
        {
            if (hq_enabled)
            {
                A_SpawnItemEx("GreenBallTail",-Vel.X,-Vel.Y,-Vel.Z,Vel.X,Vel.Y,Vel.Z,Angle,SXF_CLIENTSIDE|SXF_ABSOLUTEMOMENTUM|SXF_ABSOLUTEANGLE|SXF_ABSOLUTEPOSITION);
            }
            else
            {
                A_SpawnItem("ArachPlasmaTrail");
            }
        }
    }

    void A_ZPExplosion()
    {
        if (zp_enabled)
        {
            if (hq_enabled)
            {
                A_SetScale(0.065);
                A_SetTranslation("ZPBFG");
                A_SpawnProjectile("GreenFlare3", 0, 0, random (0, 360), CMF_AIMDIRECTION|CMF_BADPITCH, random (0, 360));
                A_SpawnItem("BFGBallImpactShockWave");
                A_SpawnItem("GreenPlasmaSmoke");
                if (Random(0,3) == 0) A_SpawnItem("GenericBolt");
                if (Random(0,3) == 0) A_SpawnItem("GenericBolt");
                if (Random(0,3) == 0) for(int i=0;i<6;i++) A_SpawnItemEx("ShockSpark",0,0,0,FRandom(0,5),0,Random(-1,5),Random(1,360),128,128);
            }
            else
            {
                A_SpawnItem("GreenPlasmaSmoke");
                A_SpawnItem("BoomGreenSmall");
	            A_SpawnItem("GreenExplosionFlareSmall");
            }
        }
    }
}