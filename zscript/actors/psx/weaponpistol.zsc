class PsxPistol : PsxDoomWeapon replaces Pistol
{
    Default
    {
		//$Title "Pistol"
		
        Weapon.SelectionOrder 1900;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 20;
		Weapon.AmmoType "Clip";
        Weapon.SisterWeapon "PoweredPsxPistol";
		Obituary "$OB_MPPISTOL";
		+WEAPON.WIMPY_WEAPON
		Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED";
		Tag "$TAG_PISTOL";

	    Weapon.SlotNumber 2;
	    Inventory.Icon "_PISA0";
        PsxDoomWeapon.VanillaSprite "PISG";
        PsxDoomWeapon.SmoothSprite "PKPI";
        PsxDoomWeapon.BlackSprite "BLPI";
        PsxDoomWeapon.ClassicSprite "2ISG";
        PsxDoomWeapon.WeaponLight "WEAPLIGHT1";
    }

	States
	{
        SwapSprites:
            PISG A 0; PKPI A 0; BLPI A 0; 2ISG A 0;
            Stop;

        Ready:
            TNT1 A 1 { A_SwapSprites(); A_WeaponReady(WRF_ALLOWUSER1); }
            Loop;

        Deselect:
            TNT1 A 1 { A_SwapSprites(); A_FastLower(); }
            Loop;
        
        Select:
            TNT1 A 1 { A_SwapSprites(); A_FastRaise(); }
            Loop;

        Fire:
            TNT1 A 0 A_SwapSprites;
		    "####" B 6 { A_FirePistol(); A_Casing(); }
            "####" C 6;
            "####" B 4 A_ReFire;
            Goto Ready;
        SmoothFire:
            TNT1 A 0 A_SwapSprites;
            "####" B 2 { A_FirePistol(); A_Casing(); A_RecoilPitch(3, 0.6); }
            "####" CE 2;
            "####" E 2 offset(0,34);
            "####" D 1 offset(0,33);
            "####" DB 1 offset(0,32);
            "####" B 1;
            "####" A 4 a_refire;
            Goto Ready;
        SmoothSlowFire:
            TNT1 A 0 A_SwapSprites;
            "####" B 2 { A_FirePistol(); A_Casing(); A_RecoilPitch(3, 0.6); }
            "####" CE 2;
            "####" E 2 offset(0,34);
            "####" D 2 offset(0,33);
            "####" D 2 offset(0,32);
            "####" B 1 offset(0,32);
            "####" B 1;
            "####" A 5 a_refire;
            Goto Ready;


        Flash:
            PISF A 6 Bright A_Light1;
            Goto LightDone;
		ClassicFlash:
			2ISF A 6 Bright A_Light1;
            Goto LightDone;
       	SmoothFlash:
            PKPF B 1 Bright A_Light2;
            PKPF A 1 Bright A_Light1;
            Goto LightDone;

        Spawn:
            PIST A -1;
            Stop;
	}
	
	override void PostBeginPlay()
	{
		if (ce_gibrules) bEXTREMEDEATH = false;
		super.PostBeginPlay();
	}

    action void A_Casing()
    {
        if (catoggle)
        {
            A_spawnitemex ("PsxBulletCasing", 40, 0, 25, random(4,6), random(7,8), random(7,9), 0);
        }
    }
}

extend class PsxDoomWeapon
{
	action void A_FirePistol()
    {
        A_GunFlash();
        A_StartSound ("weapons/pistol", CHAN_AUTO);
        A_PsxBullets();
    }
}

class PoweredPsxPistol : PsxPistol 
{
    Default
    {
        +WEAPON.POWERED_UP
        Weapon.AmmoGive 0;
        Weapon.SisterWeapon "PsxPistol";
    }

    States
    {
        Fire:
            TNT1 A 0 A_SwapSprites;
		    "####" B 6 { A_FirePistol(); A_Casing(); }
            "####" C 6;
            "####" B 4 A_ReFire;
            Goto Ready;
        SmoothFire:
            TNT1 A 0 A_SwapSprites;
            "####" B 2 { A_FirePistol(); A_Casing(); A_RecoilPitch(3, 0.6); }
            "####" CE 2;
            "####" E 2 offset(0,34);
            "####" D 1 offset(0,33);
            "####" DB 1 offset(0,32);
            "####" B 1;
            "####" A 4 a_refire;
            Goto Ready;
        SmoothSlowFire:
            TNT1 A 0 A_SwapSprites;
            "####" B 2 { A_FirePistol(); A_Casing(); A_RecoilPitch(3, 0.6); }
            "####" CE 2;
            "####" E 2 offset(0,34);
            "####" D 2 offset(0,33);
            "####" D 2 offset(0,32);
            "####" B 1 offset(0,32);
            "####" B 1;
            "####" A 5 a_refire;
            Goto Ready;
    }
    
    action void A_FirePistol()
    {
        Console.Printf("PoweredPsxPistol Fired");
        A_GunFlash();
        A_StartSound ("weapons/pistol", CHAN_AUTO);
        A_PsxBullets(SPREAD_XY, 0, 7);
    }
}