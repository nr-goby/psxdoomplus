class PsxChaingun : PsxDoomWeapon replaces Chaingun
{
    Default
    {
		//$Title "Chaingun"
		
        Weapon.SelectionOrder 700;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 20;
		Weapon.AmmoType "Clip";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Obituary "$OB_MPCHAINGUN";
		Tag "$TAG_CHAINGUN";

	    Weapon.SlotNumber 4;
	    Inventory.Icon "_MGNA0";
        PsxDoomWeapon.VanillaSprite "CHGG";
        PsxDoomWeapon.SmoothSprite "PKCG";
        PsxDoomWeapon.BlackSprite "PKCG";
        PsxDoomWeapon.ClassicSprite "5HGG";
        PsxDoomWeapon.WeaponLight "WEAPLIGHT1";
    }
	States
	{
        SwapSprites:
            CHGG A 0; PKCG A 0; 5HGG A 0;
            Stop;

        Ready:
            TNT1 A 1 { A_SwapSprites(); A_WeaponReady(WRF_ALLOWUSER1); }
            Loop;

        Deselect:
            TNT1 A 1 { A_SwapSprites(); A_FastLower(); }
            Loop;
        
        Select:
            TNT1 A 1 { A_SwapSprites(); A_FastRaise(); }
            Loop;

        Fire:
		    TNT1 A 0 A_SwapSprites;
            "####" A 4 { A_GunFlash(); A_RecoilPitch(); A_FireCGun(); A_Casing(); }
            "####" B 4 { if (invoker.previousWeaponType) { A_GunFlash("ClassicFlash2"); } else { A_GunFlash("Flash2"); } A_RecoilPitch(); A_FireCGun(); A_Casing(); }
            "####" B 0 A_ReFire;
            "####" B 0 A_ZPSmoke;
            Goto Ready;
        SmoothFire:
		    TNT1 A 0 A_SwapSprites;
            PKCG A 1 { A_GunFlash(); A_FireCGun(); A_Casing(); A_RecoilPitch(2, 1); }
            PKCG BCD 1;
            PKCG A 1 { A_GunFlash("SmoothFlash2"); A_FireCGun(); A_Casing(); A_RecoilPitch(2, 1); }
            PKCG BCD 1;
            PKCG B 0 A_ReFire;
            PKCG A 1 { A_WeaponReady(); A_ZPSmoke(); }
            PKCG BB 1 A_WeaponReady;
            PKCG CCC 1 A_WeaponReady;
            PKCG DDD 1 A_WeaponReady;
            Goto Ready;
        SmoothSlowFire:
		    TNT1 A 0 A_SwapSprites;
            PKCG A 1 { A_GunFlash(); A_FireCGun(); A_Casing(); A_RecoilPitch(2, 1); }
            PKCG ABCD 1;
            PKCG A 1 { A_GunFlash("SmoothFlash2"); A_FireCGun(); A_Casing(); A_RecoilPitch(2, 1); }
            PKCG ABCD 1;
            PKCG B 0 A_ReFire;
            PKCG A 1 { A_WeaponReady(); A_ZPSmoke(); }
            PKCG BB 1 A_WeaponReady;
            PKCG CCC 1 A_WeaponReady;
            PKCG DDD 1 A_WeaponReady;
            Goto Ready;

        Flash:
            CHGF A 3 Bright A_Light1;
            Goto LightDone;
        Flash2:
            CHGF B 2 Bright A_Light2;
            Goto LightDone;
		ClassicFlash:
			5HGF A 3 Bright A_Light1;
            Goto LightDone;
        ClassicFlash2:
            5HGF B 2 Bright A_Light2;
            Goto LightDone;
        SmoothFlash:
            PKCF A 1 Bright A_Light1;
            PKCF B 1 Bright A_Light2;
            TNT1 A 2 A_Light1;
            Goto LightDone;
        SmoothFlash2:
            PKCF C 1 Bright A_Light1;
            PKCF D 1 Bright A_Light2;
            TNT1 A 2 A_Light1;
            Goto LightDone;

        Spawn:
            MGUN A -1;
            Stop;
	}
	
	override void PostBeginPlay()
	{
		if (ce_gibrules) bEXTREMEDEATH = false;
		super.PostBeginPlay();
	}

    action void A_ZPSmoke()
    {
        if (zp_enabled)
        {
     		A_SpawnItemEx("WeaponSmoke",24,random(-1,1),sin(-pitch)*25+random(35,36),	0,0,1,0);
    		A_SpawnItemEx("WeaponSmoke",24,random(-1,1),sin(-pitch)*25+random(35,36),	0,0,1,0);
        }
    }

    action void A_Casing()
    {
        if (catoggle)
        {
    		A_spawnitemex ("PsxBulletCasing", 40, 0, 15, random(5,7), random(7,8), random(7,10), 0);
        }
    }
}

extend class PsxDoomWeapon
{
	action void A_FireCGun()
    {
        A_StartSound ("weapons/chngun", CHAN_AUTO);
        A_PsxBullets();
    }
}
