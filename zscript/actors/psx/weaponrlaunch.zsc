class PsxRocketLauncher : PsxDoomWeapon replaces RocketLauncher
{
    Default
    {
		//$Title "Rocket launcher"
		
        Weapon.SelectionOrder 2500;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 2;
		Weapon.AmmoType "RocketAmmo";
		+WEAPON.NOAUTOFIRE
		Inventory.PickupMessage "$GOTLAUNCHER";
		Tag "$TAG_ROCKETLAUNCHER";

	    Weapon.SlotNumber 5;
        Inventory.Icon "_LANA0";
        PsxDoomWeapon.VanillaSprite "MISG";
        PsxDoomWeapon.SmoothSprite "PKRL";
        PsxDoomWeapon.BlackSprite "PKRL";
        PsxDoomWeapon.ClassicSprite "6ISG";
        PsxDoomWeapon.WeaponLight "WEAPLIGHT1";
    }

    States
	{
		SwapSprites:
            MISG A 0; PKRL A 0; 6ISG A 0;
            Stop;

        Ready:
            TNT1 A 1 { A_SwapSprites(); A_WeaponReady(WRF_ALLOWUSER1); }
            Loop;

        Deselect:
            TNT1 A 1 { A_SwapSprites(); A_FastLower(); }
            Loop;
        
        Select:
            TNT1 A 1 { A_SwapSprites(); A_FastRaise(); }
            Loop;

        Fire:
		    TNT1 A 0 A_SwapSprites;
            "####" B 8 A_GunFlash;
            "####" B 12 { A_RecoilPitch(); A_FireMissile(); }
            "####" B 0 A_ReFire;
            Goto Ready;
        SmoothFire:
		    TNT1 A 0 A_SwapSprites;
            "####" A 4 A_GunFlash;
            "####" B 2;
            "####" D 1 A_FireMissile;
            "####" D 2 A_RecoilPitch(5, 1);
            "####" CC 1;
            "####" C 1;
            "####" B 2;
            "####" E 2;
            "####" F 2;
            "####" G 2;
            "####" A 0 A_ReFire;
            Goto Ready;
        SmoothSlowFire:
		    TNT1 A 0 A_SwapSprites;
            "####" A 4 A_GunFlash;
            "####" B 2;
            "####" D 2 A_FireMissile;
            "####" D 2 A_RecoilPitch(5, 1);
            "####" CC 2;
            "####" C 1;
            "####" B 2;
            "####" E 2;
            "####" F 2;
            "####" G 2;
            "####" A 0 A_ReFire;
            Goto Ready;

        Flash:
            MISF A 4 Bright A_Light1;
            MISF B 4 Bright;
            MISF CD 4 Bright A_Light2;
            Goto LightDone;
		ClassicFlash:
			6ISF A 4 Bright A_Light1;
            6ISF B 4 Bright;
            6ISF CD 4 Bright A_Light2;
			Goto LightDone;
        SmoothFlash:
            PKRF A 2 Bright A_Light1;
            PKRF B 2 Bright;
            PKRF C 2 Bright A_Light2;
            PKRF DE 3 Bright;
            Goto LightDone;

        Spawn:
            LAUN A -1;
            Stop;
	}
}

class PsxRocket : PsxProjectile replaces Rocket
{
    Default
    {
        Radius 11;
		Height 8;
		Speed 20;
		Damage 20;
		Projectile;
		//+RANDOMIZE
		+DEHEXPLOSION
		+ROCKETTRAIL
		+ZDOOMTRANS
		SeeSound "weapons/rocklf";
		DeathSound "weapons/rocklx";
		Obituary "$OB_MPROCKET";

	    +FORCEXYBILLBOARD;
        PsxActor.AmbientSound "weapons/rocketloop";
    }

    States
    {
        Spawn:
            MISL A 1 Bright { A_ZPTrail(); }
            Loop;
        Death:
            TNT1 A 0 A_StopAmbientSound;
            TNT1 A 0 a_jumpif(zp_enabled && hq_enabled, "HQDeath");
            TNT1 A 0 a_jumpif(IsSmooth(), "SmoothDeath");
            MISL B 8 Bright { A_Explode(); A_ZPExplosion(); }
            MISL C 6 Bright;
            MISL D 4 Bright;
            Stop;
        SmoothDeath:
       		expl A 5 Bright { A_Explode(); A_ZPExplosion(); }
    		expl BCDEF 3 Bright;
            Stop;
        HQDeath:
            TNT1 A 8 { A_Explode(); Radius_Quake(1,8,0,10,0); A_ZPExplosion(); }
	        TNT1 A 6 Light("ROCKET_X1");
	        TNT1 A 6 Light("ROCKET_X2");
	        TNT1 A 6 Light("ROCKET_X3");
            Stop;
    }
	
	override void PostBeginPlay()
	{
		bEXTREMEDEATH = ce_gibrules;
		super.PostBeginPlay();
	}

    void A_ZPTrail()
    {
        if (zp_enabled)
        {
            if (hq_enabled)
            {
                A_SetScale(0.25);
                A_SpawnItemEx("RocketFlare",-16,0,8,0,0,0,0,SXF_TRANSFERSCALE);
                A_SetScale(1);
            }
            else
            {
                A_SpawnItemEx("SmallRedFlare", -16, 0, 8, 0, 0, 0, 0, 176);
            }
        }
    }

    void A_ZPExplosion()
    {
        if (zp_enabled)
        {
            if (hq_enabled)
            {
                A_SetScale(0.65);
                for(int i=0;i<16 ;i++) A_SpawnItemEx("ExplosionSpark",0,0,0,FRandom(5,10),0,FRandom(-5,10),Random(1,360));
                for(int i=0;i<9 ;i++) A_SpawnItemEx("RandomExplosionSprite",0,0,0,FRandom(0,5),0,FRandom(-4,4),Random(1,360),SXF_TRANSFERSCALE);
                for(int i=0;i<9 ;i++) A_SpawnItemEx("ExplosionSmoke",0,0,0,FRandom(0,3),0,FRandom(-3,3),Random(1,360),SXF_TRANSFERSCALE);
                A_SetScale(1);
            }
            else
            {
                A_SpawnItem("RocketBoom");
                A_SpawnItemEx("DeathSpawner_Rocket",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
                A_SpawnItem("ExploFogSpawner");
                for(int i=0;i<6 ;i++) A_SpawnItemEx("AltSmokeLarge",0,0,8,0.03*Random(-50,50),0.03*Random(-50,50),0.015*Random(-100,100),0,0,0);
            }
        }
    }
}

class PsxRocketSmokeTrail : PsxProjectile replaces RocketSmokeTrail
{
    Default
    {
        RenderStyle "Translucent";
		Alpha 0.4;
		VSpeed 1;
		+NOBLOCKMAP
		+NOCLIP
		+NOGRAVITY 
		+DONTSPLASH
		+NOTELEPORT

        +NOTONAUTOMAP;
    }

    states
	{
        Spawn:
            TNT1 A 0 nodelay a_jumpif(zp_enabled, "ZPSpawn");
            SPSH ABC 8;
            SPSH D 16;
            Stop;
        ZPSpawn:
            TNT1 A 0 a_jumpif(hq_enabled, "HQSpawn");
            TNT1 AA 1 A_SpawnProjectile("BasicSmokeTrail", 8, 0, 0, CMF_AIMDIRECTION|CMF_BADPITCH, 0);
            Stop;
        HQSpawn:
            TNT1 A 1 A_SpawnItemEx("RocketSmokiTrail",0,0,8,0,FRandom(-0.5,0.5),FRandom(-0.5,0.5),0,0,64);
            Stop;
        Death:
            TNT1 A 0 a_jumpif((zp_enabled || hq_enabled), "ZPDeath");
            SPSH D 10;
            Stop;
        ZPDeath:
            TNT1 A 0;
            Stop;
	}
}