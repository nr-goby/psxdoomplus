class PsxFatso : PsxActor replaces Fatso
{
    Default
    {
		//$Title "Mancubus"
		
        Health 600;
		Radius 48;
		Height 64;
		Mass 1000;
		Speed 8;
		PainChance 80;
		Monster;
		+FLOORCLIP
		+BOSSDEATH
		SeeSound "fatso/sight";
		PainSound "fatso/pain";
		DeathSound "fatso/death";
		ActiveSound "fatso/active";
		Obituary "$OB_FATSO";
		Tag "$FN_MANCU";

	    +MOVEWITHSECTOR;
        PsxActor.FootstepSound "monster/fatsostep";
        PsxActor.DeathThudSound "monster/xvile";
        +PsxActor.WAITLOOK;
        +PsxActor.SEETARGET;
		PsxActor.NightmareMissileChanceFactor 1.2;
    }

    States
	{
        Spawn:
            FATT A 0 nodelay a_jumpif(IsSmooth(), "SmoothSpawn");
            FATT AB 14 A_Look;
            Loop;
        SmoothSpawn:
            SFAT AB 15 A_Look;
		    Loop;

        See:
            TNT1 A 0 a_jumpif(IsSmooth(), "SmoothSee");
            FATT AABBC 4 A_Chase;
            FATT C 4 { A_Chase(); A_Footstep(); }
            FATT DDEEF 4 A_Chase;
            FATT F 4 { A_Chase(); A_Footstep(); }
            Loop;
        SmoothSee:
            CRIS ABCD 4 a_chase;
            CRIS E 4 { a_chase(); A_Footstep(); }
            CRIS FGHI 4 a_chase;
            CRIS J 4 { a_chase(); A_Footstep(); }
            loop;

        Missile:
            TNT1 A 0 a_jumpif(IsSmooth(), "SmoothMissile");
            FATT G 20 A_FatRaise;
            FATT H 10 BRIGHT A_FatAttack1;
            FATT IG 4 { A_PCTics(5); A_FaceTarget(); }
            FATT H 10 BRIGHT A_FatAttack2;
            FATT IG 4 { A_PCTics(5); A_FaceTarget(); }
            FATT H 10 BRIGHT A_FatAttack3;
            FATT IG 4 { A_PCTics(5); A_FaceTarget(); }
            Goto See;
        SmoothMissile:
            FATA a 17 A_FatRaise;
            FATA b 3 A_FaceTarget;
            FATA c 3 BRIGHT A_FatAttack1;
            FATA d 3 BRIGHT A_FaceTarget;
            FATA e 4 A_FaceTarget;
            FATA a 5 A_FaceTarget;
            FATA b 3 { A_PCTics(5); A_FaceTarget(); }
            FATA c 3 BRIGHT A_FatAttack2;
            FATA d 3 BRIGHT A_FaceTarget;
            FATA e 4 A_FaceTarget;
            FATA a 5 A_FaceTarget;
            FATA b 3 { A_PCTics(5); A_FaceTarget(); }
            FATA c 3 BRIGHT A_FatAttack3;
            FATA d 3 BRIGHT A_FaceTarget;
            FATA e 4 A_FaceTarget;
            FATA a 5 A_FaceTarget;
            FATA b 3 { A_PCTics(5); A_FaceTarget(); }
            Goto See;

        Pain:
            TNT1 A 0 a_jumpif(IsSmooth(), "SmoothPain");
            FATT J 2 A_PCTics(3);
            FATT J 2 { A_PCTics(3); A_Pain(); }
            Goto See;
        SmoothPain:
            FAPP a 2 A_PCTics(3);
            FAPP b 2 { A_PCTics(3); A_Pain(); }
            goto See;

        Death:
            TNT1 A 0 a_jumpif(IsSmooth(), "SmoothDeath");
            FATT K 4;
            FATT L 4 A_Scream;
            FATT M 4 A_NoBlocking;
            FATT NO 4;
            FATT P 4 A_DeathThud;
            FATT QRS 4;
            FATT T -1 A_BossDeath;
            Stop;
        SmoothDeath:
            sman a 3;
            sman b 3 A_Scream;
            sman cde 3;
            SMAN f 3 a_noblocking;
            SMAN ghijklmnop 3;
            TNT1 A 0 A_DeathThud;
            SMAN q -1 a_bossdeath;
            stop;

        Raise:
			TNT1 A 0 a_jumpif(IsSmooth(), "SmoothRaise");
            FATT R 5;
            FATT QPONMLK 5;
            Goto See;
        SmoothRaise:
            SMAN ponmlkji 3;
            SMAN hgfedcba 3;
            Goto See;

        XDeath:
			TNT1 A 0 a_jumpif(IsSmooth(), "SmoothXDeath");
            TNT1 A 0 A_JumpIf(!kch_enabled,"Death");
            FAXD A 4;
			FAXD C 4 { A_Scream(); A_GoreExplosion(10); }
			FAXD E 4;
			FAXD G 4 A_XScream;
			FAXD I 4 A_BossDeath;
			FAXD K 4 A_NoBlocking;
			FAXD MO 4;
			FAXD P -1;
            Stop;
        SmoothXDeath:
            FAXD A 2;
			FAXD B 2 { A_Scream(); A_GoreExplosion(10); }
			FAXD C 2;
			FAXD D 2;
			FAXD E 2;
			FAXD F 2 A_XScream;
			FAXD G 2;
			FAXD H 2 A_BossDeath;
			FAXD I 2;
			FAXD J 2 A_NoBlocking;
			FAXD KLMNO 2;
			FAXD P -1;
            Stop;
	}
}

class PsxFatshot : PsxProjectile replaces Fatshot
{
    Default
	{
		Radius 6;
		Height 8;
		//Speed 20;
		Damage 8;
		Projectile;
		+RANDOMIZE
		+ZDOOMTRANS
		RenderStyle "Add";
		Alpha 1;
		SeeSound "fatso/attack";
		DeathSound "fatso/shotx";

        Speed 15;
	    FastSpeed 20;
        PsxActor.PCSpeed 20;
        PsxActor.AmbientSound "fireball/loop";
	}

	States
	{
        Spawn:
            TNT1 A 0 A_ZPSmoke;
        SpawnLoop:
            MANF A 1 BRIGHT { A_ZPFlare(); A_ZPTrail(); }
            MANF A 1 BRIGHT A_ZPTrail;
            MANF A 1 BRIGHT { A_ZPFlare(); A_ZPTrail(); }
            MANF A 1 BRIGHT A_ZPTrail;
            MANF B 1 BRIGHT { A_ZPFlare(); A_ZPTrail(); }
            MANF B 1 BRIGHT A_ZPTrail;
            MANF B 1 BRIGHT { A_ZPFlare(); A_ZPTrail(); }
            MANF B 1 BRIGHT A_ZPTrail;
            Loop;

        Death:
            TNT1 A 0 A_StopAmbientSound;
            TNT1 A 0 a_jumpif(zp_enabled && hq_enabled, "HQDeath");
            TNT1 A 0 a_jumpif(IsSmooth(), "SmoothDeath");
            MISL B 8 BRIGHT A_ZPExplosion;
            MISL C 6 BRIGHT;
            MISL D 4 BRIGHT;
            Stop;
        SmoothDeath:
            expl a 3 bright A_ZPExplosion;
            expl b 2 bright;
            expl c 2 bright;
            expl d 2 bright;
            expl e 2 bright;
            expl f 2 bright;
            Stop;
        HQDeath:
            TNT1 A 8 Light("ROCKET_X1") A_ZPExplosion;
            TNT1 A 6 Light("ROCKET_X2");
            TNT1 A 4 Light("ROCKET_X3");
            Stop;
	}

    void A_ZPSmoke()
    {
        if (zp_enabled) A_SpawnItemEx("SlowSmoke", Random(-3, 3)-8, Random(-3, 3), Random(-1, 2), (0.002)*Random(-350, 350), (0.002)*Random(-350, 350), (0.002)*Random(-350, 350), 0, 0, 64);
    }

    void A_ZPFlare()
    {
        if (zp_enabled)
        {
            if (hq_enabled)
            {
                A_SpawnItemEx("FatShotFireParticle",5,0,0,FRandom(0,3),0,FRandom(-3,3),Random(1,360));
		        A_SpawnItemEx("SlowSmoke",0,0,0,0,FRandom(-0.5,0.5),FRandom(-0.5,0.5),0,128,64);
		        A_SpawnItemEx("FatShotTrail");
            }
            else
            {
                A_SpawnItemEx("SmallRedFlare", 0, 0, 2, 0, 0, 0, 0, 0, 108);
            }
            
        }
    }

    void A_ZPTrail()
    {
        if (zp_enabled)
        {
            if (hq_enabled)
            {
                A_SpawnItemEx("RocketFlare",8,0,5);
            }
            else
            {
                A_SpawnItemEx("Trail_FatShot",frandom(-1,1),frandom(-1,1),frandom(-1,1),0,0,0,0,SXF_NOCHECKPOSITION);
            }
        }
    }

    void A_ZPExplosion()
    {
        if (zp_enabled)
        {
            if (hq_enabled)
            {
                Radius_Quake(1, 3, 0, 7, 0);
                A_SetScale(0.5);
		        for(int i=0;i<7;i++) A_SpawnItemEx("HeatBallSplash",0,0,0,FRandom(2,6),0,FRandom(0,6),Random(1,360));
                for(int i=0;i<7;i++) A_SpawnItemEx("ExplosionSpark",0,0,0,FRandom(5,10),0,FRandom(-5,10),Random(1,360));
		        for(int i=0;i<9;i++) A_SpawnItemEx("RandomExplosionSprite",0,0,0,FRandom(0,5),0,FRandom(-4,4),Random(1,360),SXF_TRANSFERSCALE);
            }
            else
            {
                A_SpawnItem("MancuBoom");
                A_SpawnItemEx("DeathSpawner_FatShot",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
                for(int i=0;i<4;i++) A_SpawnItemEx("AltSmokeXL",0,0,8,0.03*Random(-50,50),0.03*Random(-50,50),0.015*Random(-100,100),0,0,0);
            }
        }
    }
}