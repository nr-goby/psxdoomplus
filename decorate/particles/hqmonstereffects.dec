Actor SoulTrails : BlendableActor
{
	Radius 1
	Height 1
	Speed 2
	Damage 0 
    +NOBLOCKMAP
    +NOTELEPORT
    +DONTSPLASH
	+MISSILE
	+FORCEXYBILLBOARD
    +CLIENTSIDEONLY
	+NOTONAUTOMAP
	-SOLID
	RenderStyle Add
    Scale 0.5
	Alpha 0.55
	Gravity 0
	States
	{
	Spawn:
		TNT1 A 0 nodelay A_CheckSight("Inv")
		TNT1 A 2 
        FRFX KLMNOP 3 BRIGHT
    	Stop
	Inv:
		TNT1 A 0
		Stop
	}
}


actor YFire : SHTorchSpawner
{
	States
	{
	Spawn:
	    TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("TorchSmokeSmall", Random(-3, 3), Random(-3, 3), Random(13, 15), (0.003)*Random(-350, 350), (0.003)*Random(-350, 350), (0.001)*Random(1500, 3000), 0, 0, 64)
		TNT1 A 0 A_SpawnItemEx("YellowEmber", Random(-5, 5), Random(-5, 5), Random(10, 13), (0.001)*Random(10, 300), (0.001)*Random(10, 300), (0.001)*Random(2000, 4000), 0, 0, 172)
		TNT1 A 0 A_Jump(128, 2)
	   TNT1 A 1 Light("ARCHFIRE4") A_SpawnItemEx("YellowTorchFire1", Random(-3, 3), Random(-3, 3), Random(6, 9), (0.001)*Random(15, 450), (0.001)*Random(15, 450), (0.001)*Random(1800, 3600), 0, 0, 96)
	   Stop
	   TNT1 A 1 Light("ARCHFIRE6") A_SpawnItemEx("YellowTorchFire2", Random(-3, 3), Random(-3, 3), Random(6, 9), (0.001)*Random(15, 450), (0.001)*Random(15, 450), (0.001)*Random(1800, 3600), 0, 0, 96)
	   Stop
	}
}

Actor EffectBase : BlendableActor
{
	RenderStyle Add
	+NOINTERACTION
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+NOBLOCKMAP
	+DONTSPLASH
	+THRUACTORS
	+NOTRIGGER
	+NOBLOCKMONST
	+CANNOTPUSH
	+NOTONAUTOMAP
	-SOLID
	Radius 0.5
	Height 1
	Gravity 0.66
	Species "VFX"
} 
 
Actor RandomExplosionSprite : EffectBase
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetScale(-ScaleX,ScaleY)
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetScale(ScaleX,-ScaleY)
		TNT1 A 0 A_Jump(256,"Spawn1","Spawn2","Spawn3","Spawn4","Spawn5")
	Spawn1:
		X003 ACEGHJKMNPQSTUWYZ 1 Bright
		Stop
	Spawn2:
		X004 ABCDEFGHIJKLMNOPQ 1 Bright
		Stop
	Spawn3:
		X005 ACDFGHJKMOPQSUVWX 1 Bright
		Stop
	Spawn4:
		X006 ABCDEFGHIJKLMNOP 1 Bright
		Stop
	Spawn5:
		X015 ACDFGHIKLNOPRSUWX 1 Bright
		Stop
	}
} 

Actor ShockSpark : EffectBase
{
	Projectile
//	Alpha 0.75
	Speed 5
	-NOINTERACTION
	-NOGRAVITY
	+DOOMBOUNCE
	Scale 0.05
	States
	{
	Spawn:
		TNT1 A 0
		SPK1 A 3 Bright A_SetScale(ScaleX*FRandom(1,1.25))
		SPK1 A 1 Bright A_SetScale(ScaleX-0.005)
		TNT1 A 0 A_JumpIf(ScaleX<=0,"null")
		Goto Spawn+2
	}
}

Actor GenericBolt : EffectBase
{
	+BRIGHT
	States
	{
	Spawn:
		TNT1 AA 0 A_SetAngle(Random(1,360))
		TNT1 A 0 A_SetPitch(Random(-90,90))
		TNT1 A 0 A_SetScale(ScaleX*FRandom(0.8,1.25))
	Anim:
		NULL AAA 1 A_SetScale(ScaleX/2)
		Stop
	}
}
Actor GreenBolt : GenericBolt
{
	Translation "ZPBFG"
}

Actor PlasmaBallTail : EffectBase
{
	Translation "0:255=195:195", "80:111=[128,128,255]:[0,0,0]", "5:8=0:0", "3:3=207:207", "0:0=0:0"
	Alpha 0.5
	States
	{
	Spawn:
		TNT1 A 0
		NULL A 1 BRIGHT
		Stop
	}
}

Actor GreenBallTail : PlasmaBallTail
{
	Translation "0:255=%[0,0,0]:[1.5,2,0.85]"
}

Actor PlasmaBallImpactShockWave : EffectBase
{
	Scale 0.33
	Translation "0:255=%[0,0,0]:[0,0.5,1.5]"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(ScaleX*FRandom(0.5,1.1))
		X060 H 1 Bright A_FadeOut(0.2)
		TNT1 A 0 A_SetScale(ScaleX*1.5)
		Goto Spawn+1
	}
}
Actor BFGBallImpactShockWave : EffectBase
{
	Scale 0.33
	Translation "ZPBFG"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(ScaleX*FRandom(0.5,1.1))
		X060 H 1 Bright A_FadeOut(0.2)
		TNT1 A 0 A_SetScale(ScaleX*1.5)
		Goto Spawn+1
	}
}

Actor FFFlame : EffectBase
{
	Alpha 0.6
	Scale 0.8
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(ScaleX*0.15)
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetScale(-ScaleX,ScaleY)
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetScale(ScaleX,-ScaleY)
		TNT1 A 0 A_Jump(256,"Spawn1","Spawn2","Spawn3","Spawn4","Spawn5")
	Spawn1:
		X052 ABCDEFGHIJKLMNOPQRS 1 Bright
		Stop
	Spawn2:
		X053 ABCDEFGHIJKLMNOPQRSTU 1 Bright
		Stop
	Spawn3:
		X054 ABCDEFGHIJKLMNOPQRS 1 Bright
		Stop
	Spawn4:
		X055 ABCDEFGHIJKLMNO 1 Bright
		Stop
	Spawn5:
		X056 ABCDEFGHIJKLMNOPQR 1 Bright
		Stop
	}
}

Actor FatShotTrail : EffectBase
{
	Scale 0.08
	States
	{
	Spawn:
		TNT1 A 2
	Decide:
		E002 ABCD 0 Bright A_Jump(64,"Anim")
		Loop
	Anim:
		"----" A 1 Bright A_JumpIf(ScaleX<=0,"Null")
		"----" A 0 A_SetScale(ScaleX-0.0133)
		Loop
	}
}
Actor FatShotFireParticle : EffectBase
{
	Scale 0.5
	States
	{
	Spawn:
		P001 H 1 Bright A_JumpIf(ScaleX<=0,"Null")
		TNT1 A 0 A_SetScale(ScaleX-0.05)
		Loop
	}
}
Actor ImpBallFlame : FatShotTrail
{
	Scale 0.033
	Translation "0:255=%[0,0,0]:[1.9,1,0.4]"
	States
	{
	Anim:
		"----" A 1 Bright A_JumpIf(ScaleX<=0,"Null")
		"----" A 0 A_SetScale(ScaleX-0.01)
		Loop
	}
}
Actor NImpBallFlame : ImpBallFlame { Translation "ZPNightmare" }
Actor CacoDemonBallTrail : ImpBallFlame { Translation "0:255=%[0,0,0]:[1.5,0.5,1]" }
Actor CacoBallExplosionSprite : RandomExplosionSprite { Scale 0.33 Translation "0:255=%[0,0,0]:[1,0.25,0.66]" }
Actor NImpExplosionSprite : RandomExplosionSprite { Translation "ZPNightmare" }
Actor BaronBallTrailSpawner : EffectBase
{
	Translation "0:255=%[0,0,0]:[0.66,1.5,0.66]"
	Scale 1.75
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx(RocketFlare,0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|1)
		TNT1 A 0 A_SpawnItemEx(FFFLame,FRandom(0,10),0,FRandom(-5,5),FRandom(0,2),0,FRandom(-1,1),Random(1,360),SXF_TRANSFERSCALE|SXF_TRANSFERPOINTERS|1)
		Stop
	}
}
Actor ExplosionSpark : EffectBase
{
	Scale 0.1
	+MISSILE +BOUNCEONWALLS
	-NOINTERACTION
	Gravity 0.66
	Speed 10
	States
	{
	Spawn:
		SPK1 A Random(5,15) Bright A_SetScale(ScaleX*FRandom(0.75,1.1))
	Fade:
		"####" "#" 1 Bright A_SetScale(ScaleX*0.925)
		"####" "#" 0 A_JumpIf(ScaleX<0.005,"Null")
		Loop
	}
}
Actor ExplosionSparkGreen : ExplosionSpark
{
	States
	{
	Spawn:
		SPK1 C Random(5,15) Bright A_SetScale(ScaleX*FRandom(0.75,1.1))
		Goto Fade
	}
}
Actor ExplosionSmoke : EffectBase
{
	Speed 4
	Radius 4
	Height 4
	alpha 0.45
	RenderStyle Translucent
	Var Int User_Scale;
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetUserVar(User_Scale,ScaleX<<16)
		Decide:
		X037 ABCD 0 A_Jump(32,"Anim")
		Loop
	Anim:
		"####" "#" 0 A_SetScale(ScaleX+((User_Scale*0.0000153)*0.01))
		"####" "#" 0 A_ScaleVelocity(0.96)
		"####" "#" 1 A_FadeOut(0.0045)
		Loop
	}
}
Actor RocketSmokiTrail : EffectBase
{
	RenderStyle Add
	Alpha 0.25
	Scale 0.2
	States
	{
	Spawn:
		TNT1 AA 0 A_SetScale(ScaleX*FRandom(0.75,1.1))/*
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetScale(-ScaleX,ScaleY)
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetScale(ScaleX,-ScaleY)*/
	Decide:
		PFFU EFG 0 A_Jump(96,"Anim")
		Loop
	Anim:
		"----" A 1 A_FadeOut(0.01)
		"----" A 0 A_SetScale(ScaleX*1.025)
		Loop
	}
}

Actor HeatBallSplash : EffectBase
{
	Translation "168:191=[255,255,0]:[255,0,0]", "16:47=[255,255,0]:[255,0,0]"
	Scale 0.35
	-NOINTERACTION
	Gravity 0.33
	Alpha 0.99
	States
	{
	Spawn:
		TNT1 AA 0 A_SetScale(ScaleX*FRandom(0.75,1.2))
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetScale(-ScaleX,ScaleY)
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetScale(ScaleX,-ScaleY)
		TNT1 A 0 A_Jump(256,"Anim1","Anim2","Anim3")
	Anim1:
		X057 ABBCDDEFFGHHIJJKKLMMNOOPQQRSS 1 Bright A_FadeOut(0.03)
		Stop
	Anim2:
		X058 ABBCDDEFFGHHIJJKKLMMN 1 Bright A_FadeOut(0.045)
		Stop
	Anim3:
		X059 ABBCDDEFFGHHIJJKKLMM 1 Bright A_FadeOut(0.045)
		Stop
	}
}

Actor GaussCannonParticle : EffectBase
{
	Scale 0.2
	States
	{
	Spawn:
		TNT1 A 0 A_FadeOut(0.1)
		MUZZ H 1 Bright A_SetScale(ScaleX*0.95)
		Loop
	}
}


Actor VortexSmoke : EffectBase
{
	RenderStyle Add
	Translation "0:255=%[0,0,0]:[0.5,0.5,1]"
	Scale 0.5
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(128,2)
		TNT1 A 0 A_SetScale(-ScaleX,ScaleY)
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetScale(ScaleX,-ScaleY)
		TNT1 A 0 A_Jump(256,"Anim")
	Anim:
		PFFU F 1 A_SetScale(ScaleX*1.0166)
		TNT1 A 0 A_FadeOut(0.01)
		Loop
	}
}

Actor GaussCannonSmoke : VortexSmoke
{
	Scale 0.5
	Translation "0:255=%[0,0,0]:[0.5,0.75,1]"
	States
	{
	Anim:
		PFFU F 1 Bright A_SetScale(ScaleX*1.025)
		TNT1 A 0 A_FadeOut(0.025)
		Loop
	}
}

Actor GaussCannonExplosionSpark : GaussCannonParticle
{
	+DONTSPLASH +MISSILE -NOINTERACTION +THRUACTORS
	Radius 1
	Height 1
	Scale 0.33
	Translation "0:255=%[0.00,0.00,0.00]:[1.07,1.65,2.00]"
	Gravity 0.33
	Speed 7
	States
	{
	Spawn:
		TNT1 AA 0 A_ScaleVelocity(FRandom(0.75,1.1))
		TNT1 A 0 A_SetScale(ScaleX*FRandom(0.75,1.2))
		NULL A 5 Bright
	Fade:
		NULL A 1 Bright A_JumpIf(ScaleX<=0,"Null")
		TNT1 A 0 A_SetScale(ScaleX-0.02)
		Loop
	}
}

Actor BFGBallExpBoltSpawner : EffectBase
{
	Translation "0:255=%[0,0,0]:[0.00,1.01,0.50]"
	Scale 6
	Radius 58
	States
	{
	Spawn:
		TNT1 AAAAAAAAA 1 A_SpawnItemEx(GenericBolt,0,0,0,0,0,0,0,SXF_TRANSFERSCALE|1)
	Fading:
		TNT1 A 1 A_SpawnItemEx(GenericBolt,0,0,0,0,0,0,0,SXF_TRANSFERSCALE|1)
		TNT1 A 0 A_SetScale(ScaleX-0.25)
		TNT1 A 0 A_JumpIf(ScaleX<=0,"Null")
		Loop
	}
}

Actor BulletPuffSpark : ShockSpark {}
Actor BulletPuffGlow : EffectBase
{
	Scale 0.15
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(ScaleX*1.5)
		LENY AAAAA 1 Bright A_SetScale(ScaleX*0.9)
		Stop
	}
}

Actor BFGBallTrailSpawner : EffectBase
{
	Translation "0:255=%[0,0,0]:[0.5,1,0.2]"
	Scale 1.5
	Projectile
	-NOINTERACTION -CLIENTSIDEONLY
	States
	{
	Spawn:
		TNT1 A 0 A_SpawnItemEx(BFGBallGlow,0,0,0,-VelX,-VelY,-VelZ,0,SXF_ABSOLUTEMOMENTUM|SXF_CLIENTSIDE)
		TNT1 AA 0 A_SpawnItemEx(GenericBolt,-VelX*2,-VelY*2,-VelZ*2,VelX,VelY,VelZ,Angle,SXF_ABSOLUTEPOSITION|SXF_ABSOLUTEMOMENTUM|1|SXF_ABSOLUTEANGLE|SXF_TRANSFERSCALE|SXF_CLIENTSIDE,128)
		TNT1 A 0 A_SpawnItemEx(BFGBallTrail,0,0,0,FRandom(0,0.5),0,FRandom(-0.5,0.5),Random(1,360),SXF_CLIENTSIDE)
		Stop
	}
}
Actor BFGBallGlow : EffectBase
{
	Translation "0:255=%[0,0,0]:[1.5,2,1.24]"
	Alpha 0.33
	States
	{
	Spawn:
		TNT1 AA 0 A_SetScale(ScaleX*FRandom(0.75,1.125))
		P001 M 1 Bright
		Stop
	}
}
	
Actor BFGBallTrail : EffectBase
{
	Alpha 0.2
	Scale 0.5
	Translation "0:255=%[0,0,0]:[0.5,1.25,0.25]"
	States
	{
	Spawn:
		TNT1 A 3
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetScale(-ScaleX,ScaleY)
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetScale(ScaleX,-ScaleY)
	Fading:
		TNT1 A 0 A_SetScale(ScaleX*0.95)
		P001 K 1 Bright A_FadeOut(0.01)
		Loop
	}
}

Actor GaussCannonExplosionShockWave : GaussCannonParticle
{
	Translation "0:255=%[0,0,0]:[0.78,1.54,1.95]"
	Scale 0.1
	States
	{
	Spawn:
		BSW1 A 1 Bright A_FadeOut(0.066)
		TNT1 A 0 A_SetScale(ScaleX*1.15)
		Loop
	}
}
Actor BFGExplosionShockWave : GaussCannonExplosionShockWave
{
	Translation "0:255=%[0,0,0]:[0,1,0]"
	Scale 0.15
	Health 80
	Alpha 0.5
	States
	{
	Spawn:
		BSW1 A 1 Bright A_FadeOut(0.03)
		TNT1 A 0 A_SetScale(ScaleX+(Health*0.001))
		TNT1 AAAA 0 A_GetHurt
		Loop
	}
}
Actor BFGExplosionSpark : GaussCannonExplosionSpark {Scale 0.33 Speed 8 Alpha 0.5 Translation "0:255=%[0,0,0]:[1.5,2,0.85]" }
Actor BFGExplosionSprite : RandomExplosionSprite { Scale 1.5 Alpha 0.5 Translation "0:255=%[0,0,0]:[1.5,2,0.85]" }
Actor BFGExplosionSmoke : GaussCannonSmoke
{
	Translation "0:255=%[0,0,0]:[1.25,1.5,0.75]"
	Scale 0.66
	Alpha 0.5
	States
	{
	Anim:
		PFFU F 1 Bright A_SetScale(ScaleX*1.01)
		TNT1 A 0 A_FadeOut(0.005)
		Loop
	}
}

Actor MicroMissileSmokeTrail : RocketSmokiTrail { Scale 0.1 }